//noinspection GradleDependency
buildscript {
    ext.min_sdk_version = "8.2.0"
}

plugins {
    id "com.github.node-gradle.node" version "2.2.4"
}
apply plugin: "com.android.library"
apply plugin: "maven-publish"
apply plugin: "checkstyle"

android {
    compileSdk 35

    defaultConfig {
        minSdkVersion 24
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 35
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lint {
        abortOnError false
    }
    namespace 'com.acmerobotics.dashboard'

}

node {
    version = "18.12.1"
    download = true
    nodeModulesDir = file("${project.projectDir}/dash")
}

yarn_build.dependsOn yarn_install

tasks.register('cleanDashAssets', Delete) {
    delete "${android.sourceSets.getByName("main").assets.srcDirs[0]}/dash"
}

clean.dependsOn cleanDashAssets

tasks.register('copyDashAssets', Copy) {
    from "${project.projectDir}/dash/dist"
    into "${android.sourceSets.getByName("main").assets.srcDirs[0]}/dash"
}

copyDashAssets.dependsOn cleanDashAssets
copyDashAssets.dependsOn yarn_build

android.libraryVariants.configureEach { variant ->
    variant.preBuildProvider.get().dependsOn copyDashAssets
}

// adapted from https://github.com/Genymobile/gnirehtet/blob/master/config/android-checkstyle.gradle
check.dependsOn "checkstyle"

checkstyle {
    toolVersion = checkstyle_version
}

tasks.register('checkstyle', Checkstyle) {
    description = "Check Java style with Checkstyle"
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    source = javaSources()
    classpath = files()
    ignoreFailures = true
}

// I"d like to replace each() with collectMany() if only I knew how
// Also, the static suggestion here is wrong but I lack the incantation to suppress it
def javaSources() {
    def files = []
    android.sourceSets.each { sourceSet ->
        sourceSet.java.each { javaSource ->
            javaSource.getSrcDirs().each {
                if (it.exists()) {
                    files.add(it)
                }
            }
        }
    }
    return files
}

repositories {
    flatDir {
        dirs "../libs"
    }
    mavenCentral()
}

// Probably not worth combining with addPomDependencies()
// Keep them in sync manually
dependencies {
    api(project(":DashboardCore")) {
        transitive = false
    }

    api "org.firstinspires.ftc:RobotCore:9.2.0"
    api "org.firstinspires.ftc:Hardware:9.2.0"
    api "org.firstinspires.ftc:RobotServer:9.2.0"
    api "org.firstinspires.ftc:FtcCommon:9.2.0"

    implementation "androidx.annotation:annotation:1.8.1"

    implementation("org.nanohttpd:nanohttpd-websocket:2.3.1") {
        exclude module: "nanohttpd"
    }
}

def addPomDependencies(root) {
    def depsNode = root.appendNode("dependencies")

    // Apparently you can make closures but not "inner methods"
    def addDep = { groupId, artifactId, version ->
        def n = depsNode.appendNode("dependency")
        n.appendNode("groupId", groupId)
        n.appendNode("artifactId", artifactId)
        n.appendNode("version", version)
        return n
    }

    addDep("com.acmerobotics.dashboard", "core", dashboard_version)

    ["RobotCore", "Hardware", "RobotServer", "FtcCommon"].forEach {
        addDep("org.firstinspires.ftc", it, min_sdk_version)
    }

    addDep("androidx.annotation", "annotation", "1.1.0")

    addDep("org.nanohttpd", "nanohttpd-websocket", "2.3.1")
            .appendNode("exclusions")
            .appendNode("exclusion")
            .with {
                appendNode("groupId", "org.nanohttpd")
                appendNode("artifactId", "nanohttpd")
            }
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

tasks.register('androidJavadocs', Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

tasks.register('androidJavadocsJar', Jar) {
    dependsOn androidJavadocs
    archiveClassifier = "javadoc"
    from androidJavadocs.destinationDir
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifact bundleReleaseAar
                artifact androidSourcesJar
                artifact androidJavadocsJar

                groupId = "com.acmerobotics.dashboard"
                artifactId = "dashboard"
                version dashboard_version

                pom {
                    description = "Web dashboard designed for FTC"
                    name = "FTC Dashboard"
                    url = "https://github.com/acmerobotics/ftc-dashboard"

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "https://opensource.org/licenses/MIT"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "rbrott"
                            name = "Ryan Brott"
                            email = "rcbrott@gmail.com"
                        }
                    }

                    scm {
                        url = "https://github.com/acmerobotics/ftc-dashboard"
                    }
                }

                // Seems like you can"t define ad hoc software components without a plugin.
                // Remind me never to read the Gradle docs again.
                pom.withXml {
                    // Android Studio complains here, but passing null straight up doesn't work
                    //noinspection GroovyImplicitNullArgumentCall
                    def root = asNode()
                    addPomDependencies(root)
                }
            }
        }
    }
}