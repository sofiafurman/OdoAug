/**
 * Top-level build file for ftc_app project.
 *
 * It is extraordinarily rare that you will ever need to edit this file.
 */
configurations {
    doc { transitive false }
}

buildscript {
    ext.dashboard_version = '0.4.14'
    ext.checkstyle_version = '8.18'

    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        // Note for FTC Teams: Do not modify this yourself.
        classpath 'com.android.tools.build:gradle:8.4.2'
    }
}

// This is now required because aapt2 has to be downloaded from the
// google() repository beginning with version 3.2 of the Android Gradle Plugin
allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

repositories {
    mavenCentral()

    flatDir {
        dirs '../libs'
    }
}

dependencies {
    doc 'org.firstinspires.ftc:Hardware:9.2.0'
    doc 'org.firstinspires.ftc:RobotCore:9.2.0'
    doc 'org.firstinspires.ftc:FtcCommon:9.2.0'
    doc 'org.firstinspires.ftc:OnBotJava:9.2.0'
    doc 'org.firstinspires.ftc:Inspection:9.2.0'
}

tasks.register('extractJavadoc') {
    doLast {
        def componentIds = configurations.doc.incoming.resolutionResult.allDependencies.collect { it.selected.id }

        def result = dependencies.createArtifactResolutionQuery()
                .forComponents(componentIds)
                .withArtifacts(JvmLibrary, SourcesArtifact, JavadocArtifact)
                .execute()

        for (component in result.resolvedComponents) {
            component.getArtifacts(JavadocArtifact).each { artifact ->
                def version = artifact.identifier.componentIdentifier.version
                def libName = artifact.identifier.componentIdentifier.moduleIdentifier.name
                copy {
                    from zipTree(artifact.file)
                    into "docs/$version/$libName/"
                }
            }
        }
    }
}
